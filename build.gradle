plugins {
    id 'org.springframework.boot' version '2.2.3.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'groovy'
    id 'maven'
    id 'maven-publish'
}

group = 'com.crosscountry.ea.common'
version = '0.0.2'
sourceCompatibility = '1.8'

ext {
    dotenv = { def props = new Properties(); props.load(new File("../.env").newInputStream()); return props }.call()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.codehaus.groovy:groovy:$groovyVersion"
    // Hibernate Search
    implementation "org.hibernate:hibernate-search-engine:${hibernateSearchVersion}", {
        exclude module: 'jboss-logging'
    }
    implementation "org.hibernate:hibernate-search-orm:${hibernateSearchVersion}", {
        exclude module: 'jboss-logging'
    }
    implementation "org.hibernate:hibernate-search-elasticsearch:${hibernateSearchVersion}", {
        exclude module: 'jboss-logging'
    }
    // Swagger / OpenAPI
    implementation "org.springdoc:springdoc-openapi:${springdocVersion}"

    // SLF4J
    implementation "org.slf4j:slf4j-api"
}

jar {
    enabled = true
}

bootJar {
    enabled = false
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            if (System.getenv("CI_JOB_TOKEN")) {
                version = project.version
            } else {
                version = "${project.version}-SNAPSHOT".toString()
            }
            from components.java
        }
    }
    repositories {
        maven {
            url "https://gitlab.com/api/v4/projects/17195343/packages/maven"
            if (System.getenv("CI_JOB_TOKEN")) {
                credentials(HttpHeaderCredentials) {
                    name = "Job-Token"
                    value = System.getenv("CI_JOB_TOKEN")
                }
            } else {
                credentials(HttpHeaderCredentials) {
                    name = "Private-Token"
                    value = dotenv.get("GITLAB_PRIVATE_TOKEN")
                }
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}